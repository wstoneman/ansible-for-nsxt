locals {
  sec_groups = yamldecode(file("${path.module}/input_test_file.yaml.txt"))["sec_groups"]
}

resource "nsxt_policy_group" "group" {
  for_each     = { for sg in local.sec_groups : sg["display_name"] => sg }
  display_name = each.value["display_name"]
  description  = try(each.value["id"], null)
  domain       = "default"

  # For complex groups, loop through expressions
  dynamic "criteria" {
    for_each = [
      for expr in each.value["expression"] :
      {
        type        = try(expr["resource_type"], "")
        expressions = try(expr["expressions"], null)
        paths       = try(expr["paths"], null)
        ipaddrs     = try(expr["ip_addresses"], null)
        key         = try(expr["key"], null)
        member_type = try(expr["member_type"], null)
        operator    = try(expr["operator"], null)
        value       = try(expr["value"], null)
      } if contains(
        ["NestedExpression", "Condition", "IPAddressExpression", "PathExpression"], 
        try(expr["resource_type"], expr["key"])
      )
    ]
    content {
      # Nested Expression or single Condition (tags)
      dynamic "condition" {
        for_each = criteria.value.type == "NestedExpression" ? [
          for subexpr in criteria.value.expressions :
          subexpr if try(subexpr["resource_type"], "") == "Condition"
        ] : criteria.value.type == "Condition" ? [criteria.value] : []
        content {
          key         = condition.value["key"]
          member_type = condition.value["member_type"]
          operator    = condition.value["operator"]
          value       = condition.value["value"]
        }
      }
      # Static IPs
      dynamic "ipaddress_expression" {
        for_each = criteria.value.type == "IPAddressExpression" && criteria.value.ipaddrs != null ? [
          { ip_addresses = criteria.value.ipaddrs }
        ] : []
        content {
          ip_addresses = ipaddress_expression.value.ip_addresses
        }
      }
      # DIRECT SEGMENT MEMBERSHIP (PathExpression as path_expression block inside criteria)
      dynamic "path_expression" {
        for_each = criteria.value.type == "PathExpression" && criteria.value.paths != null ? [
          { member_paths = criteria.value.paths }
        ] : []
        content {
          member_paths = path_expression.value.member_paths
        }
      }
    }
  }
}
